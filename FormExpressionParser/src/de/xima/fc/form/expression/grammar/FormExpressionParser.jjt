PARSER_BEGIN(FormExpressionParser)
package de.xima.fc.form.expression.grammar;
import de.xima.fc.form.expression.enums.EMethod;
import de.xima.fc.form.expression.enums.ELogLevel;
import org.apache.commons.lang3.StringUtils;
import de.xima.fc.form.expression.node.*;

/** Formcycle expression language.
    http://ramkulkarni.com/blog/handling-some-of-the-warnings-and-errors-generated-by-javacc/
 */
public class FormExpressionParser
{
  /** Main entry point. */
  public static void main(String args []) throws ParseException
  {
    System.out.println("Use as a library...");
  }
}

PARSER_END(FormExpressionParser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

Node Program() #StatementListNode :
{
}
{
  (    (Clause(null))+ | < EOF >
  )
  {
    jjtThis.init(null);
    return jjtThis;
  }
}

void Clause(EMethod method) #void :
{
}
{
 (
    ForLoop(method)
  | IfClause(method)
  | WhileLoop(method)
  | SwitchClause(method)
  | TryClause(method)
  | WithClause(method)
  | ThrowClause(method)
  | BreakClause(method)
  | ContinueClause(method)
  | Statement(method, false)
  )
}

void WithClause(EMethod method) #WithClauseNode :
{
}
{
  (
    < With > "(" (IdentifierName(null))+ ")" BlockOrClause(null)  )
  {
    jjtThis.init(method);
  }
}

void ContinueClause(EMethod method) #ContinueClauseNode :
{
    Token t = null;
}
{
  (
    < Continue > [ t = < IDENTIFIER > ]";"
  )
  {
    jjtThis.init(method, t == null ? null : t.image);
  }
}

void BreakClause(EMethod method) #BreakClauseNode :
{
  Token t = null;
}
{
  (
    < Break > [ t = < IDENTIFIER > ]";"  )
  {
    jjtThis.init(method, t == null ? null : t.image);
  }
}

void ThrowClause(EMethod method) #ThrowClauseNode :
{
}
{
  (
    < Throw > Exception(null) ";"  )
  {
    jjtThis.init(method);
  }
}

void SwitchClause(EMethod method) #SwitchClauseNode :
{
}
{
  (
    < Switch > "(" Expression(null) ")" "{"
    (
      SwitchCase()
    )*
    [
      SwitchDefault()
    ]
    "}"
  )
  {
    jjtThis.init(method);
  }
}

void SwitchCase() #void :
{
}
{
  (
    (
      < Case > Expression(EMethod.SWITCHCASE) ":"
    )+
    (Clause(EMethod.SWITCHCLAUSE))+
  )
}

void SwitchDefault() #void :
{
}
{
  (    < Default > ":"
      (Clause(EMethod.SWITCHDEFAULT))+
  )
}

void TryClause(EMethod method) #TryClauseNode :
{
  Token t;
}
{
  (
    < Try >
    (
      Block(null)
    )
    < Catch > "(" t = < IDENTIFIER > ")"
    (
      Block(null)
    )
  )
  {
    jjtThis.init(method, t == null ? null : t.image);
  }
}

void DoWhileLoop(EMethod method) #DoWhileLoopNode :
{
}
{
  (
    < Do >
    BlockOrClause(null)
    < While > "(" Expression(null) ")"
  )
  {
    jjtThis.init(method);
  }
}

void WhileLoop(EMethod method) #WhileLoopNode :
{
}
{
  (
    < While > "(" Expression(null) ")"
    BlockOrClause(null)
  )
  {
    jjtThis.init(method);
  }
}

void ForLoop(EMethod method) #ForLoopNode :
{
  Token iteratingLoopVariable = null;
}
{
  (
    < For > "("
    (
      LOOKAHEAD(< IDENTIFIER > ":")
      (
        iteratingLoopVariable = < IDENTIFIER > ":" Expression(null)
      )
    |
      (
        StatementListCommaSeparated(null) ";" StatementListCommaSeparated(null) ";" StatementListCommaSeparated(null)
      )
    )
    ")"
    BlockOrClause(null)
  )
  {
    jjtThis.init(method, iteratingLoopVariable != null ? iteratingLoopVariable.image : null);
  }
}

void IfClause(EMethod method) #IfClauseNode :
{
}
{
  (
    < If > "(" Expression(null) ")" 
    BlockOrClause(null)
    [
      LOOKAHEAD(1)
      < Else > 
      BlockOrClause(null)
    ]
  )
  {
    jjtThis.init(method);
  }
}

void BlockOrClause(EMethod method) #void :
{
}
{
  LOOKAHEAD("{")
  Block(method)
| Clause(method)
}

void Block(EMethod method) #StatementListNode :
{
}
{
  (
    "{"
    (
      (
        Clause(null)
      )*
    )
    "}"
  )
  {
    jjtThis.init(method);
  }
}

void StatementListCommaSeparated(EMethod method) #StatementListNode :
{
}
{
  (
    (Expression(null))? ("," Expression(null))*
  )
  {
    jjtThis.init(method);
  }
}

Node Statement(EMethod method, boolean createNode) #ExpressionNode(createNode) :
{
}
{
  (
    (
      Expression(null) ";" | ";"
    )
  )
  {
    if (!createNode) return null;
    if (jjtThis.jjtGetNumChildren() > 0) jjtThis.init(method);
    return jjtThis;
  }
}

void Expression(EMethod method) #void :
{
}
{
  EqualTypeExpression(method)
}

void EqualTypeExpression(EMethod method) #AssignmentExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
}
{
  (
    DoubleBarExpression(method)
    (
      (
        "=" DoubleBarExpression(EMethod.EQUAL)
      )
    |
      (
        "+=" DoubleBarExpression(EMethod.PLUS_EQUAL)
      )
    |
      (
        "-=" DoubleBarExpression(EMethod.DASH_EQUAL)
      )
    |
      (
        "**=" DoubleBarExpression(EMethod.DOUBLE_STAR_EQUAL)
      )
    |
      (
        "*=" DoubleBarExpression(EMethod.STAR_EQUAL)
      )
    |
      (
        "/=" DoubleBarExpression(EMethod.SLASH_EQUAL)
      )
    |
      (
        "%=" DoubleBarExpression(EMethod.PERCENT_EQUAL)
      )
    |
      (
        "<<=" DoubleBarExpression(EMethod.DOUBLE_ANGLE_OPEN_EQUAL)
      )
    |
      (
        ">>=" DoubleBarExpression(EMethod.DOUBLE_ANGLE_CLOSE_EQUAL)
      )
    |
      (
        "&&=" DoubleBarExpression(EMethod.DOUBLE_AMPERSAND_EQUAL)
      )
    |
      (
        "||=" DoubleBarExpression(EMethod.DOUBLE_BAR_EQUAL)
      )
    |
      (
        "&=" DoubleBarExpression(EMethod.AMPERSAND_EQUAL)
      )
    |
      (
        "^=" DoubleBarExpression(EMethod.CIRCUMFLEX_EQUAL)
      )
    |
      (
        "|=" DoubleBarExpression(EMethod.BAR_EQUAL)
      )
    )*
  )
  {
    if (createNode) jjtThis.init(method, this);
  }
}

void DoubleBarExpression(EMethod method) #ExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
}
{
  (
    DoubleAmpersandExpression(method)
    (
      "||" DoubleAmpersandExpression(EMethod.DOUBLE_BAR)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void DoubleAmpersandExpression(EMethod method) #ExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
  //EMethod nextMethod = createNode? null : method;
}
{
  (
    BarExpression(method)
    (
      "&&" BarExpression(EMethod.DOUBLE_AMPERSAND)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void BarExpression(EMethod method) #ExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
  //EMethod nextMethod = createNode? null : method;
}
{
  (
    CircumflexExpression(method)
    (
      "|" CircumflexExpression(EMethod.BAR)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void CircumflexExpression(EMethod method) #ExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
  //EMethod nextMethod = createNode? null : method;
}
{
  (
    AmpersandExpression(method)
    (
      "^" AmpersandExpression(EMethod.CIRCUMFLEX)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void AmpersandExpression(EMethod method) #ExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
  //EMethod nextMethod = createNode? null : method;
}
{
  (
    DoubleEqualTypeExpression(method)
    (
      "&" DoubleEqualTypeExpression(EMethod.AMPERSAND)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void DoubleEqualTypeExpression(EMethod method) #ExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
  //EMethod nextMethod = createNode? null : method;
}
{
  (
    AngleTypeExpression(method)
    (
      (
        "===" AngleTypeExpression(EMethod.TRIPLE_EQUAL)
      )
    |
      (
        "==" AngleTypeExpression(EMethod.DOUBLE_EQUAL)
      )
    |
      (
        "!=" AngleTypeExpression(EMethod.EXCLAMATION_EQUAL)
      )
    |
      (
        "!==" AngleTypeExpression(EMethod.EXCLAMATION_DOUBLE_EQUAL)
      )
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void AngleTypeExpression(EMethod method) #ExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
  //EMethod nextMethod = createNode? null : method;
}
{
  (
    PlusLikeExpression(method)
    (
      (
        "<" PlusLikeExpression(EMethod.ANGLE_OPEN)
      )
    |
      (
        ">" PlusLikeExpression(EMethod.ANGLE_CLOSE)
      )
    |
      (
        "<=" PlusLikeExpression(EMethod.ANGLE_OPEN_EQUAL)
      )
    |
      (
        ">=" PlusLikeExpression(EMethod.ANGLE_CLOSE_EQUAL)
      )
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void PlusLikeExpression(EMethod method) #ExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
  //EMethod nextMethod = createNode? null : method;
}
{
  (
    StarLikeExpression(method)
    (
      (
        "+" StarLikeExpression(EMethod.PLUS)
      )
    |
      (
        "-" StarLikeExpression(EMethod.DASH)
      )
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void StarLikeExpression(EMethod method) #ExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
  //  EMethod nextMethod = createNode? null : method;
}
{
  DoubleStarExpression(method)
  (
    (
      (
        "*" DoubleStarExpression(EMethod.STAR)
      )
    |
      (
        "/" DoubleStarExpression(EMethod.SLASH)
      )
    |
      (
        "%" DoubleStarExpression(EMethod.PERCENT)
      )
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void DoubleStarExpression(EMethod method) #ExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode;
}
{
  (
    UnaryExpression(method)
    [
      "**" DoubleStarExpression(EMethod.DOUBLE_STAR)
    ]
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void UnaryExpression(EMethod method) #UnaryExpressionNode(unaryMethod != null) :
{
  EMethod unaryMethod = null;
}
{
  (
    (
      "++" UnaryExpression(method)
      {
        unaryMethod = EMethod.DOUBLE_PLUS_PREFIX;
      }
    )
  |
    (
      "--" UnaryExpression(method)
      {
        unaryMethod = EMethod.DOUBLE_DASH_PREFIX;
      }
    )
  |
    (
      "+" UnaryExpression(method)
      {
        unaryMethod = EMethod.PLUS_UNARY;
      }
    )
  |
    (
      "-" UnaryExpression(method)
      {
        unaryMethod = EMethod.DASH_UNARY;
      }
    )
  |
    (
      "!" UnaryExpression(method)
      {
        unaryMethod = EMethod.EXCLAMATION;
      }
    )
  |
    PropertyExpression(method)
  )
  {
    if (unaryMethod != null) jjtThis.init(method, unaryMethod);
  }
}

void PropertyExpression(EMethod method) #PropertyExpressionNode(createNode = jjtree.nodeArity() > 1) :
{
  boolean createNode = true;
}
{
  (
    FunctionTypeUnaryExpression(method)
    (
      "." IdentifierName(EMethod.DOT)
    | "[" Expression(EMethod.BRACKET) "]"
    | "(" FunctionArgumentList(EMethod.PARENTHESIS) ")"
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void IdentifierName(EMethod method) #IdentifierNameNode :
{
  Token t;
}
{
  (    t = < IDENTIFIER >
  )
  {
    jjtThis.init(method, t.image);
  }
}

void FunctionArgumentList(EMethod method) #ArrayNode :
{
}
{
  (
    [ Expression(null) ]
    (
      "," Expression(null)
    )*
  )
  {
    jjtThis.init(method);
  }
}

void FunctionTypeUnaryExpression(EMethod method) #void :
{
  boolean createNode;
}
{
  (
    (
      Literal(method)
    | 
      VariableWithOptionalScope(method)
    |
      (
        "(" Expression(method) ")"
      )
    )
  )
  {
  }
}

void VariableWithOptionalScope(EMethod method) #VariableNode :
{
  Token part1;
  Token part2 = null;
}
{
  (
    part1 = < IDENTIFIER > ["::" part2 = < IDENTIFIER >]
  )
  {
    if (part2 ==null)
      jjtThis.init(method, null, part1.image);
    else
      jjtThis.init(method, part1.image, part2.image);
  }
}

void Number(EMethod method) #NumberNode :
{
  Token t;
  boolean isInt;
}
{
  (
    t = < Integer >
    {
      isInt = true;
    }
  | 
    t = < Float >
    {
      isInt = false;
    }
  )
  {
    jjtThis.init(method, t.image, isInt);
  }
}

void String(EMethod method) #StringNode :
{
  Token t;
  char delimiter;
}
{
  (
    t = < SingleQuotedString >
    {
      delimiter = '\'';
    }
  | 
    t = < DoubleQuotedString >
    {
      delimiter = '"';
    }
  )
  {
    jjtThis.init(method, t.image, delimiter);
  }
}

void Array(EMethod method) #ArrayNode :
{
}
{
  (
    "["
    (
      "]"
    |
      (
        Expression(null)
        (
          "," Expression(null)
        )*
        "]"
      )
    )
  )
  {
    jjtThis.init(method);
  }
}

void Hash(EMethod method) #HashNode :
{
}
{
  (
    "{"
    (
      "}"
    |
      (
        HashEntry(null)
        (
          "," HashEntry(null)
        )*
        "}"
      )
    )
  )
  {
    jjtThis.init(method);
  }
}

void HashEntry(EMethod method) #void :
{
}
{
  Literal(null) ":" Expression(null)
}

void Null(EMethod method) #NullNode :
{
}
{
  < Null >
  {
    {
      jjtThis.init(method);
    }
  }
}

void Boolean(EMethod method) #BooleanNode :
{
  boolean b;
}
{
  (
    < True >
    {
      b = true;
    }
  | 
    < False >
    {
      b = false;
    }
  )
  {
    jjtThis.init(method, b);
  }
}

void Log(EMethod method) #LogNode :
{
  ELogLevel logLevel;
}
{
  (
    (
      "\u00a7loginfo"
      {
        logLevel = ELogLevel.INFO;
      }
    | "\u00a7logerror"
      {
        logLevel = ELogLevel.ERROR;
      }
    | "\u00a7logdebug"
      {
        logLevel = ELogLevel.DEBUG;
      }
    | "\u00a7logwarn"
      {
        logLevel = ELogLevel.WARN;
      }
    )
    "(" Expression(null) ")"
  )
  {
    jjtThis.init(method, logLevel);
  }
}

void Exception(EMethod method) #ExceptionNode :
{
}
{
  (    "\u00a7exception" "(" Expression(null) ")"
  )
  {
    jjtThis.init(method);
  }
}

void Function(EMethod method) #FunctionNode :
{
}
{
  (
    "->" "("
    (
      [ IdentifierName(null) ]
      (
        "," IdentifierName(null)
      )*
    )
    ")"
    (
      Block(null)
    )
  )
  {
    jjtThis.init(method);
  }
}

// i Pattern.CASE_INSENSITIVE
// m Pattern.MULTILINE
// s Pattern.DOTALL
/*
void Regex(EMethod method) #RegexNode :
{
  Token t;
  Token flags = null;
}
{
  (
    t = < SlashQuotedString > [flags = < RegexFlags >]  )
  {
    jjtThis.init(method, t.image, flags == null ? StringUtils.EMPTY : flags.image);
  }
}
*/

void Literal(EMethod method) #void :
{
}
{
  Array(method)
| Hash(method)
| String(method)
| Number(method)
| Boolean(method)
| Null(method)
| Log(method)
| Function(method)
| Exception(method)
}

TOKEN :
{
  < DoubleQuotedString :
    "\""
    (
      ("\\" ~[ ])
    | ~[ "\\", "\"" ]
    )*
    "\"" >
| < SingleQuotedString :
    "'"
    (
      ("\\" ~[ ])
    | ~[ "\\", "'" ]
    )*
    "'" >
|
  < Integer : ([ "0"-"9" ])+ >
| 
  < Float :
    ([ "0"-"9" ])+ "." ([ "0"-"9" ])* (< EXPONENT >)?
  | "." ([ "0"-"9" ])+ (< EXPONENT >)?
  | ([ "0"-"9" ])+ < EXPONENT > 
  >
| 
  < #EXPONENT : [ "e", "E" ] ([ "+", "-" ])? ([ "0"-"9" ])+ >
}

TOKEN :
{
  < With : "with" >
| 
  < Break : "break" >
|
  < Continue : "continue" >
|
  < If : "if" >
|
  < Else : "else" >
|
  < For : "for" >
|
  < While : "while" >
|
  < Do : "do" >
|
  < Try : "try" >
|
  < Catch : "catch" >
|
  < Throw : "throw" >
|
  < Switch : "switch" >
|
  < Case : "case" >
|
  < Default : "default" >
|
  < True : "true" >
|
  < False : "false" >
|
  < Null : "null" >
| 
  < IDENTIFIER : [ "a"-"z", "A"-"Z", "_" ] ([ "a"-"z", "A"-"Z", "_", "0"-"9" ])* >
}
