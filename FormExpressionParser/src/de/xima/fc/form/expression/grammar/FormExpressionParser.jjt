PARSER_BEGIN(FormExpressionParser)
package de.xima.fc.form.expression.grammar;
import java.io.ByteArrayInputStream;
import java.nio.charset.Charset;
import org.apache.commons.lang3.StringUtils;
import de.xima.fc.form.expression.util.InfixValue;
import java.math.BigInteger;
import de.xima.fc.form.expression.util.EFunctionType;
import de.xima.fc.form.expression.exception.NoSuchFunctionException;
import de.xima.fc.form.expression.node.*;
import de.xima.fc.form.expression.util.EMethod;

/** Formcycle expression language.
    http://ramkulkarni.com/blog/handling-some-of-the-warnings-and-errors-generated-by-javacc/
 */
public class FormExpressionParser
{
  /** Main entry point. */
  public static void main(String args []) throws ParseException
  {
    System.out.println("Use as a library...");
  }
}

PARSER_END(FormExpressionParser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

/** Root for non-embedded code. */
MySimpleNode Plain() #ExpressionNode :
{
}
{
  (
    (
      Expression() < EOF >
    )
  | < EOF >
  )
  {
    jjtThis.init(null);
    return jjtThis;
  }
}

void Statement() #void :
{
}
{
  Expression() ";"
}


void Expression() #void :
{
}
{
  EqualTypeExpression(null)
}

void ExpressionWithMethodPassthrough(EMethod method) #void :
{
}
{
  EqualTypeExpression(method)
}


void EqualTypeExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
}
{
  (
    DoubleBarExpression(nextMethod)
    (
      (
        "=" DoubleBarExpression(EMethod.EQUAL)
      )
    |
      (
        "+=" DoubleBarExpression(EMethod.PLUS_EQUAL)
      )
    |
      (
        "-=" DoubleBarExpression(EMethod.DASH_EQUAL)
      )
    |
      (
        "**=" DoubleBarExpression(EMethod.DOUBLE_STAR_EQUAL)
      )
    |
      (
        "*=" DoubleBarExpression(EMethod.STAR_EQUAL)
      )
    |
      (
        "/=" DoubleBarExpression(EMethod.SLASH_EQUAL)
      )
    |
      (
        "%=" DoubleBarExpression(EMethod.PERCENT_EQUAL)
      )
    |
      (
        "<<=" DoubleBarExpression(EMethod.DOUBLE_ANGLE_OPEN_EQUAL)
      )
    |
      (
        ">>=" DoubleBarExpression(EMethod.DOUBLE_ANGLE_CLOSE_EQUAL)
      )
    |
      (
        "&&=" DoubleBarExpression(EMethod.DOUBLE_AMPERSAND_EQUAL)
      )
    |
      (
        "||=" DoubleBarExpression(EMethod.DOUBLE_BAR_EQUAL)
      )
    |
      (
        "&=" DoubleBarExpression(EMethod.AMPERSAND_EQUAL)
      )
    |
      (
        "^=" DoubleBarExpression(EMethod.CIRCUMFLEX_EQUAL)
      )
    |
      (
        "|=" DoubleBarExpression(EMethod.BAR_EQUAL)
      )
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

/*
void TrinaryExpression() #TrinaryExpressionNode :
{
}
{
  Expression() "?" Expression() ":" Expression()
  {
    jjtThis.init();
  }
}
*/

void DoubleBarExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
  }
{
  (
    DoubleAmpersandExpression(nextMethod)
    (
      "||" DoubleAmpersandExpression(EMethod.DOUBLE_BAR)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void DoubleAmpersandExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
}
{
  (
    BarExpression(nextMethod)
    (
      "&&" BarExpression(EMethod.DOUBLE_AMPERSAND)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void BarExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
}
{
  (
    CircumflexExpression(nextMethod)
    (
      "|" CircumflexExpression(EMethod.BAR)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void CircumflexExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
}
{
  (
    AmpersandExpression(nextMethod)
    (
      "^" AmpersandExpression(EMethod.CIRCUMFLEX)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void AmpersandExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
}
{
  (
    DoubleEqualTypeExpression(nextMethod)
    (
      "&" DoubleEqualTypeExpression(EMethod.AMPERSAND)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void DoubleEqualTypeExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
}
{
  (
    AngleTypeExpression(nextMethod)
    (
      (
        "===" AngleTypeExpression(EMethod.TRIPLE_EQUAL)
      )
    |
      (
        "==" AngleTypeExpression(EMethod.DOUBLE_EQUAL)
      )
|
      (
        "!=" AngleTypeExpression(EMethod.EXCLAMATION_EQUAL)
      )
    |
      (
        "!==" AngleTypeExpression(EMethod.EXCLAMATION_DOUBLE_EQUAL)
      )
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void AngleTypeExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
}
{
  (
    PlusLikeExpression(nextMethod)
    (
      (
        "<" PlusLikeExpression(EMethod.ANGLE_OPEN)
      )
    |
      (
        ">" PlusLikeExpression(EMethod.ANGLE_CLOSE)
      )
    |
      (
        "<=" PlusLikeExpression(EMethod.ANGLE_OPEN_EQUAL)
      )
    |
      (
        ">=" PlusLikeExpression(EMethod.ANGLE_CLOSE_EQUAL)
      )
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void PlusLikeExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
}
{
  (
    StarLikeExpression(nextMethod)
    (
      (
        "+" StarLikeExpression(EMethod.PLUS)
      )
    |
      (
        "-" StarLikeExpression(EMethod.DASH)
      )
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void StarLikeExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
}
{
  UnaryExpression(nextMethod)
  (
    (
      DoubleStarExpression(nextMethod)
    |
      (
        "*" UnaryExpression(EMethod.STAR)
      )
    |
      (
        "/" UnaryExpression(EMethod.SLASH)
      )
    |
      (
        "%" UnaryExpression(EMethod.PERCENT)
      )
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void UnaryExpression(EMethod method) #ExpressionNode(unaryMethod != null) :
{
  EMethod unaryMethod = null;
}
{
  (
    (
      "++" UnaryExpression(null) { unaryMethod = EMethod.DOUBLE_PLUS_PREFIX; }
    )
  |
    (
      "--" UnaryExpression(null) { unaryMethod = EMethod.DOUBLE_DASH_PREFIX; }
    )
  |
    (
      "+" UnaryExpression(null) { unaryMethod = EMethod.PLUS_UNARY; }
    )
  |
    (
      "-" UnaryExpression(null) { unaryMethod = EMethod.DASH_UNARY; }
    )
  |
    (
      "!" UnaryExpression(null) { unaryMethod = EMethod.EXCLAMATION; }
    )
  | DoubleStarExpression(method)
  )
  {
    if (unaryMethod != null) jjtThis.init(method, unaryMethod);
  }
}

void DoubleStarExpression(EMethod method) #ExpressionNode(createNode) :
{
  final boolean createNode = method != null || jjtree.nodeArity() > 1;EMethod nextMethod = createNode ? null  : method;
}
{
  (
    DotExpression(nextMethod)
    (
      (
        "**" UnaryExpression(EMethod.CIRCUMFLEX)
      )?
    )
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void DotExpression(EMethod method) #ExpressionNode(createNode) :
{
  boolean createNode = true;
  EMethod nextMethod = method;
}
{
  (
    FunctionTypeUnaryExpression(nextMethod)
    (
      "." FunctionTypeUnaryExpression(EMethod.DOT)
    )*
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void FunctionTypeUnaryExpression(EMethod method) #ExpressionNode(createNode) :
{
  boolean createNode = true;
}
{
  (
    Literal()
  |
    (
      FunctionCall()
    )
  |
    (
      "(" ExpressionWithMethodPassthrough(method) ")"
    )
  )
  {
    if (createNode) jjtThis.init(method);
  }
}

void FunctionCall() #void :
{
}
{
  (
    LOOKAHEAD(< IDENTIFIER > "(")
    ParenthesesFunction()
  | 
    PlainFunction()
  )
}

void ParenthesesFunction() #ParenthesesFunction :
{
  Token t;
}
{
  (
    t = < IDENTIFIER > "(" FunctionArgumentList() ")"
  )
  {
    jjtThis.init(t.image);
  }
}

void FunctionArgumentList() #void :
{
}
{
  (
    [ Expression() ]
    (
      "," Expression()
    )*
  )
}

void String() #void :
{
}
{
  (
    SingleQuotedString()
  | 
    DoubleQuotedString()
  )
}

void Number() #NumberNode :
{
  Token t;
  boolean isInt;
}
{
  (
    t = < Integer >
    {
      isInt = true;
    }
  | 
    t = < Float >
    {
      isInt = false;
    }
  )
  {
    jjtThis.init(t.image, isInt);
  }
}

void DoubleQuotedString() #DoubleQuotedString :
{
  Token t;
}
{
  t = < DoubleQuotedString >
  {
    jjtThis.init(t.image);
  }
}

void SingleQuotedString() #SingleQuotedString :
{
  Token t;
}
{
  t = < SingleQuotedString >
  {
    jjtThis.init(t.image);
  }
}

void Array() #ArrayNode :
{
}
{
  (
    "["
    (
      "]"
    |
      (
        Expression()
        (
          "," Expression()
        )*
        "]"
      )
    )
  )
  {
    jjtThis.init();
  }
}

void Hash() #HashNode :
{
}
{
  (
    "{"
    (
      "}"
    |
      (
        HashEntry()
        (
          "," HashEntry()
        )*
        "}"
      )
    )
  )
  {
    jjtThis.init();
  }
}

void HashEntry() #void :
{
}
{
  Expression() ":" Expression()
}

/**
 * Literal for the null object.
 */
void Null() #NullNode :
{
}
{
  "\u00a7null"
  {
    {
      jjtThis.init();
    }
  }
}

void Boolean() #BooleanNode :
{
  boolean b;
}
{
  (
    "\u00a7true"
    {
      b = true;
    }
  | 
    "\u00a7false"
    {
      b = false;
    }
  )
  {
    jjtThis.init(b);
  }
}

void Literal() #void :
{
}
{
  Array()
| Hash()
| String()
| Number()
| Boolean()
| Null()
}

void PlainFunction() #PlainFunction :
{
  Token t;
}
{
  t = < IDENTIFIER >
  {
    jjtThis.init(t.image);
  }
}

TOKEN :
{
  < DoubleQuotedString :
    "\""
    (
      ("\\" ~[ ])
    | ~[ "\\", "\"" ]
    )*
    "\"" >
| < SingleQuotedString :
    "'"
    (
      ("\\" ~[ ])
    | ~[ "\\", "'" ]
    )*
    "'" >
| 
  < Integer : ([ "0"-"9" ])+ >
| 
  < Float :
    ([ "0"-"9" ])+ "." ([ "0"-"9" ])* (< EXPONENT >)?
  | "." ([ "0"-"9" ])+ (< EXPONENT >)?
  | ([ "0"-"9" ])+ < EXPONENT > 
  >
| 
  < #EXPONENT : [ "e", "E" ] ([ "+", "-" ])? ([ "0"-"9" ])+ >
}

TOKEN :
{
  < IDENTIFIER : [ "a"-"z", "A"-"Z", "_" ] ([ "a"-"z", "A"-"Z", "_", "0"-"9" ])* >
}
